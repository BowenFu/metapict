#lang racket
(require "../def.rkt" pict racket/draw)

(provide save-pict-as-pdf)

(def a4-width  595) ; in pt
(def a4-height 842) ; in pt

(define (use-a4-paper)
  (define setup (new ps-setup%))
  (current-ps-setup setup)
  (send setup set-paper-name "A4 210 x 297 mm")
  ; introduce margins such that the result is centered
  (def α 0.9)
  (send setup set-margin (/ (* (- 1 α) a4-width) 2) (/ (* (- 1 α) a4-height) 2))
  (send setup set-editor-margin 0 0)
  (send setup set-scaling α α))

(define (new-pdf-dc filename)
  (use-a4-paper)
  (send (current-ps-setup) set-file filename)
  (define dc (new pdf-dc% [interactive #f] [parent #f] [use-paper-bbox #t] [as-eps #f] [output #f]))
  (send dc set-smoothing 'smoothed) ; important!
  dc)

(define (start dc)
  (send dc start-doc "Generated by MetaPict")
  (send dc start-page))

(define (end dc)
  (send dc end-page)
  (send dc end-doc))

(define (new-page dc)
  (send dc end-page)
  (send dc start-page))

(define (save-pict-as-pdf p filename)
  (def dc (new-pdf-dc filename))
  (start dc)
  (draw-pict p dc 0 0)
  (end dc))  